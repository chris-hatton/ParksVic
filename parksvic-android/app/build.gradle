apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.chrishatton.parksvic"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Android
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:$android_support_version"
    compile 'com.google.android.gms:play-services-maps:11.0.1'

    compile 'com.google.maps.android:android-maps-utils:0.5+'

    def withoutAndroidProvidedXmlModules = {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // The following sub-project dependencies all transitively reference simple-xml
    // and we must block the even more deeply transitive inclusion of the Xml modules
    // that are already provided by Android.

    compile project(':opengis-client-kotlin'        ), withoutAndroidProvidedXmlModules
    compile project(':opengis-client-kotlin-rx'     ), withoutAndroidProvidedXmlModules
    compile project(':opengis-client-kotlin-android'), withoutAndroidProvidedXmlModules
    compile project(':parksvic'                     ), withoutAndroidProvidedXmlModules

    compile project(':geojson-kotlin')
    compile project(':crosswind')
    compile project(':crosswind-android')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // OkHttp
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Retrofit
    compile "com.google.code.gson:gson:$gson_version"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // ReactiveX
    compile "io.reactivex.rxjava2:rxkotlin:$rx_version"
    compile "io.reactivex.rxjava2:rxjava:$rx_version"
    compile "com.jakewharton.rxbinding2:rxbinding-kotlin:$rx_binding_version"
    compile "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    compile 'com.sothree.slidinguppanel:library:3.3.1'
}
