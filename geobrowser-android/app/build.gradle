apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "org.chrishatton.geobrowser"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Android
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"

    //Google
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'


    def withoutAndroidProvidedXmlModules = {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // The following sub-project dependencies all transitively reference simple-xml
    // and we must block the even more deeply transitive inclusion of the Xml modules
    // that are already provided by Android.
    implementation project(':opengis-client-kotlin'), withoutAndroidProvidedXmlModules
    implementation project(':opengis-client-kotlin-rx'), withoutAndroidProvidedXmlModules
    implementation project(':opengis-client-kotlin-android'), withoutAndroidProvidedXmlModules
    implementation project(':geobrowser'), withoutAndroidProvidedXmlModules
    implementation project(':geojson-kotlin')
    implementation project(':crosswind')
    implementation project(':crosswind-android')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Retrofit
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // ReactiveX
    implementation "io.reactivex.rxjava2:rxkotlin:$rx_version"
    implementation "io.reactivex.rxjava2:rxjava:$rx_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rx_binding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rx_binding_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    implementation 'com.sothree.slidinguppanel:library:3.3.1'
}
